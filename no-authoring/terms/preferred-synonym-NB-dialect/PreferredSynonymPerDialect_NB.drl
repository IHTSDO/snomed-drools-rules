import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.helper.ConceptHelper

global java.util.List invalidContent

// Language code: no
// Dialect Language Refset: 61000202103
// Dialect Label: nb (just for display and naming of this file)

rule "New Norwegian concepts must have one Preferred Synonym in the NB dialect."
	dialect "mvel"
	when
		// When the concept is active
		c : Concept(active)

		// And when there are any descriptions with language code 'no' or the concept is in the Norway namespace
		d : ArrayList(size > 0 || c.moduleId contains "1000202") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "no")
		)

		// And when there is not 1 active NB PT.
		ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "no", acceptabilityMap["61000202103"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("ddf05fea-ad7a-4016-b9f2-8d070b09155a",
				c, "A concept must have one Preferred Synonym in the nb dialect.", ConceptHelper.isCoreModule(c.moduleId) && !ConceptHelper.isAllComponentsBelongToCoreModule(d));
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
