import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent

global java.util.List invalidContent

rule "New Danish concepts must have a Preferred Synonym in Danish."
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId contains "1000005") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "da")
		)
		ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "da", acceptabilityMap["554461000005103"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("e890ccdc-417b-41cc-b9c4-f56b3f79c53c",
				c, "A concept must have one Preferred Synonym in the DK dialect.");
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
