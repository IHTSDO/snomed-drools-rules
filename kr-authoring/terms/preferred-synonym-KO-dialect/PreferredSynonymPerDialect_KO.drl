import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity
import org.ihtsdo.drools.helper.ConceptHelper

global java.util.List invalidContent

rule "New Korean concepts must have a Preferred Synonym in Korean."
	dialect "mvel"
	when
		c : Concept(active)
		d : ArrayList(size > 0 || c.moduleId == "11000267109") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, moduleId == "11000267109")
		)
			
		ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["21000267104"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("24f4e7c7-e9c0-40af-9573-d5d5552abc7e",
				c, "A concept must have one Preferred Synonym in the KO dialect.", ConceptHelper.isCoreModule(c.moduleId) && !ConceptHelper.isAllComponentsBelongToCoreModule(d), Severity.ERROR);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
