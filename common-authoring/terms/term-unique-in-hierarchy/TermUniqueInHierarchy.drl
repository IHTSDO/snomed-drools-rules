import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.service.DescriptionService
import org.ihtsdo.drools.service.ConceptService
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent
global DescriptionService descriptionService
global ConceptService conceptService

// "Correlation ID" is a special case where duplicates are expected
rule "Active term unique in hierarchy"
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(c.id == d.conceptId && active && term.length > 1 && !term.startsWith("Correlation ID"))
		$duplicate : Description(active && id != d.id && conceptService.isActive(conceptId) == true) from descriptionService.findMatchingDescriptionInHierarchy(c, d)
	then
		invalidContent.add(new InvalidContent("25334385-83b3-4531-8e6e-cf9e32ebccd2",
				d, "Term already exists within this hierarchy on concept " + $duplicate.conceptId +
				". Two descriptions in the same hierarchy may only have the same term if they have different language codes or one description or concept is inactive.", Severity.WARNING));
end
