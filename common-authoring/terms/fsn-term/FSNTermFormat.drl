import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.service.ConceptService
import org.ihtsdo.drools.service.DescriptionService
import java.util.ArrayList

global java.util.List invalidContent
global ConceptService conceptService
global DescriptionService descriptionService

rule "An active FSN should not open with a parenthesis."
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && term matches "\\(.*")
	then
		invalidContent.add(new InvalidContent("9d8e8567-9e64-4386-ac7c-f79c1590f024",
				d, "An FSN should not start with open parentheses.", Severity.WARNING));
end

rule "FSN ends in closing parentheses"
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && term matches ".*[^\\)]")
	then
		invalidContent.add(new InvalidContent("d6cfc1b3-1030-460d-a3d8-4ac3bc7b1743",
				d, "An FSN must end in closing parentheses."));
end

rule "An active FSN must end with a semantic tag."
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && (term not matches ".*\\(.+\\)"))
	then
		invalidContent.add(new InvalidContent("c32401d8-b7fd-43de-b79e-5c80c9601afa",
				d, "An active FSN must end with a semantic tag."));
end

rule "An active US-English or GB-English FSN should end with a valid semantic tag."
	dialect "mvel"
	when
		c : Concept(active, id != "138875005")
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && (acceptabilityMap["900000000000509007"] == Constants.ACCEPTABILITY_PREFERRED || acceptabilityMap["900000000000508004"] == Constants.ACCEPTABILITY_PREFERRED) && !descriptionService.isRecognisedSemanticTag(DescriptionHelper.getTag(d.term), d.languageCode))
	then
		invalidContent.add(new InvalidContent("b786d30d-59ac-4e8d-b9d9-ab653bc48d57",
				d, "Active FSN should end with a valid semantic tag.", Severity.WARNING));
end

rule "An active FSN should not contain the special characters &, %, $, @ or #."
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && term matches ".*[&%$@#].*")
	then
		invalidContent.add(new InvalidContent("e3048fa9-4238-48f1-a836-1b91ed4fcec2",
				d, "An active FSN should not contain the special characters &, %, $, @ or #.", Severity.WARNING));
end

rule "The first letter of an active US-English or GB-English FSN should be capitalized."
	dialect "mvel"
	when
		c : Concept(active)
		d : Description(conceptId == c.id && active == true && typeId == Constants.FSN && (acceptabilityMap["900000000000509007"] == Constants.ACCEPTABILITY_PREFERRED || acceptabilityMap["900000000000508004"] == Constants.ACCEPTABILITY_PREFERRED) && term matches "[a-z].*")
	then
		invalidContent.add(new InvalidContent("8a74edc2-ebbe-4c59-992b-d80ea72a11ba",
				d, "The first letter of an active FSN should be capitalized.", Severity.WARNING));
end
