import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity
import org.ihtsdo.drools.helper.ConceptHelper

global java.util.List invalidContent

rule "An active concept must have one active FSN in the US-English dialect."
	dialect "mvel"
	when
		c : Concept(active)
		descriptions: ArrayList(size != 1) from collect(Description() from DescriptionHelper.filterByActiveTypeAndDialectPreferred(c, true, Constants.FSN, Constants.US_EN_LANG_REFSET))
	then
		InvalidContent invalid = new InvalidContent("dbeb9123-f2a0-4aa8-89b2-c49ef5ade27c",
				c, "An active concept should have one active FSN in the US-EN dialect.", ConceptHelper.isCoreModule(c.moduleId) && !ConceptHelper.isAllComponentsBelongToCoreModule(descriptions));
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
