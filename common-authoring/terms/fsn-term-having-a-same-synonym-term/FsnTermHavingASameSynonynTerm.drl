import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity
import org.ihtsdo.drools.domain.Constants
import java.util.ArrayList

global java.util.List invalidContent

rule "For each active FSN there is a synonym that has the same text."
	dialect "mvel"
	when
		c : Concept(active)
		fsn : Description(active, conceptId == c.id, typeId == Constants.FSN && (acceptabilityMap["900000000000509007"] == Constants.ACCEPTABILITY_PREFERRED || acceptabilityMap["900000000000508004"] == Constants.ACCEPTABILITY_PREFERRED) && DescriptionHelper.getTag(term)
						not in ("product", "medicinal product", "medicinal product form", "clinical drug", "substance", "product name", "packaged clinical drug", "real clinical drug",
						"real medicinal product", "real packaged clinical drug", "supplier"))
		ArrayList(size > 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size == 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, term.equals(DescriptionHelper.getFsnTerm(fsn.term)))
		)
	then
		InvalidContent invalid = new InvalidContent("65adcfee-cca5-4e99-8441-acc53344a140",
				c, "For each active FSN there is a synonym that has the same text.", Severity.WARNING);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
