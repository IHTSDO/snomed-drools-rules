import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Active terms sharing first word with case-sensitive term should share case sensitivity."
	dialect "mvel"
	when
		c : Concept(active)
		d1 : Description(active, conceptId == c.id)
		d2 : Description(id != d1.id 
						&& active
						&& conceptId == c.id
						&& typeId != Constants.TEXT_DEFINITION
						&& d1.typeId != Constants.TEXT_DEFINITION
						&& DescriptionHelper.getFirstWord(d1.term) == DescriptionHelper.getFirstWord(term)
						&& (caseSignificanceId == Constants.ENTIRE_TERM_CASE_SENSITIVE || d1.caseSignificanceId == Constants.ENTIRE_TERM_CASE_SENSITIVE) 
						&& d1.caseSignificanceId != caseSignificanceId)
	then
		invalidContent.add(new InvalidContent("d007641a-a124-4096-84fe-d2e09dcb7f40",
				d1, "Active terms sharing first word with case-sensitive term should share case sensitivity.", Severity.WARNING));
end
