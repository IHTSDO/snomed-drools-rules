import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "There must not be two active descriptions with the same term and language code on an active concept."
	dialect "mvel"
	when
		c : Concept(active)
		d1 : Description(active)
		d2 : Description(active && conceptId == d1.conceptId && id != d1.id && d1.languageCode == d2.languageCode && term.equals(d1.term))
	then
		invalidContent.add(new InvalidContent("f3c7ded3-cb80-41ba-8864-1cb8c1585957",
				d2, "There is an active descriptions with the same term and language code on this concept." +
			 "Please remove one of the descriptions.", Severity.ERROR));
end

rule "Active descriptions must not have the same term as an inactive description on the same concept, unless the language code is different."
	dialect "mvel"
	when
		c : Concept(active)
		d1 : Description(!active && released)
		d2 : Description(active && !released && conceptId == d1.conceptId && id != d1.id &&
						d1.languageCode == d2.languageCode && d1.moduleId == d2.moduleId && typeId == d1.typeId && term.equals(d1.term))
	then
		invalidContent.add(new InvalidContent("f0cd848f-4050-4d2d-a572-76fb5ef83374",
				d2, "There is an inactive description with the same term and language code on this concept. " +
			 "Please make the other description active and remove this one.", Severity.ERROR));
end

rule "Active case sensitive descriptions with different case significance should not have the same term."
	dialect "mvel"
	when
		c : Concept(active)
		d1 : Description(active && caseSignificanceId != Constants.ENTIRE_TERM_CASE_INSENSITIVE)
		d2 : Description(active && conceptId == d1.conceptId && id != d1.id && caseSignificanceId != Constants.ENTIRE_TERM_CASE_INSENSITIVE && caseSignificanceId != d1.caseSignificanceId &&
						d1.languageCode == d2.languageCode && !term.equals(d1.term) && term.equalsIgnoreCase(d1.term))
	then
		invalidContent.add(new InvalidContent("eb7eaf62-d900-4199-beec-a843f657dfa3",
				d2, "Active case sensitive descriptions with different case significance should not have the same term. " +
			 "There is another description with the same term but different case significance.", Severity.WARNING));
end
