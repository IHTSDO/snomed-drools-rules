import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "TextDefinition case significance"
	dialect "mvel"
	when
		d : Description(active && textDefinition && caseSignificanceId != Constants.ENTIRE_TERM_CASE_SENSITIVE)
	then
		invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: text definitions should be case-sensitive.", Severity.WARNING));
end

rule "TextDefinition first character case"
	dialect "mvel"
	when
		d : Description(active && textDefinition && term matches "[^A-Z].*")
	then
		invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: the first character of a text definition should be upper case.", Severity.WARNING));
end

rule "Preferred TextDefinition per dialect"
	dialect "mvel"
	when
		c : Concept(active && (
			DescriptionHelper.filterByActiveTypeAndDialectPreferred(c, true, Constants.TEXT_DEFINITION, Constants.US_EN_LANG_REFSET).size() > 1
			|| DescriptionHelper.filterByActiveTypeAndDialectPreferred(c, true, Constants.TEXT_DEFINITION, Constants.GB_EN_LANG_REFSET).size() > 1)
		)
	then
		InvalidContent invalid = new InvalidContent(c, "The system has detected a contradiction of the following convention: a concept may only have one Preferred text definition in each dialect.");
        invalid.ignorePublishedCheck();
        invalidContent.add(invalid);
end

rule "TextDefinition preferred"
	dialect "mvel"
	when
		d : Description(active && textDefinition && DescriptionHelper.isAcceptabilityValuePresentOnDescription(d, Constants.ACCEPTABILITY_ACCEPTABLE))
	then
		InvalidContent invalid = new InvalidContent(d, "The system has detected a contradiction of the following convention: text definitions must always be preferred.");
        invalid.ignorePublishedCheck();
        invalidContent.add(invalid);
end

rule "TextDefinition has acceptability map"
	dialect "mvel"
	when
		d : Description(active && textDefinition && !DescriptionHelper.hasAcceptabilityMap(d))
	then
		InvalidContent invalid = new InvalidContent(d, "The system has detected a contradiction of the following convention: text definitions must be preferred in at least one dialect.");
        invalid.ignorePublishedCheck();
        invalidContent.add(invalid);
end
