import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Term Case Significance: Only initial character case sensitive"
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ONLY_INITIAL_CHARACTER_CASE_INSENSITIVE && term not matches "^..*[A-Z].*$")
	then
		invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: an active term on an active concept with case significance 'only initial character case insensitive' should contain a capital letter after the first character", Severity.WARNING));
end

rule "Term Case Significance: Entire term case insensitive"
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && term matches "^..*[A-Z].*$")
	then
		invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: an active term on an active concept with case significance 'entire term case insensitive' should not contain capital letters after the first character", Severity.WARNING));
end

