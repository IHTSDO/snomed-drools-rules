import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Term Case Significance: Only initial character case sensitive"
	dialect "mvel"
	when
		c : Concept(active == true)
		d1 : Description(conceptId == c.id && active && typeId == Constants.FSN && DescriptionHelper.getTag(term) != "substance")
		d2 : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ONLY_INITIAL_CHARACTER_CASE_INSENSITIVE
				&& term not matches "^..*[A-Z].*$" && term not matches "^..*[0-9][A-Za-z].*$")
	then
		invalidContent.add(new InvalidContent(d2, "An active term on an active concept with case significance 'only initial character case insensitive' should contain " +
				"a capital letter after the first character.", Severity.WARNING));
end

rule "Term Case Significance: Entire term case insensitive"
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && term matches "^..*[A-Z].*$")
	then
		invalidContent.add(new InvalidContent(d, "An active term on an active concept with case significance 'entire term case insensitive' should not contain " +
				"capital letters after the first character.", Severity.WARNING));
end

rule "Term Case Significance: Terms containing a capital letter are expected to be marked cI or CS"
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && term matches "^..*[A-Z].*$")
	then
		invalidContent.add(new InvalidContent(d, "Terms containing a capital letter are expected to be marked cI or CS.", Severity.WARNING));
end
