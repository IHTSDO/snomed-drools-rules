import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.Severity
import java.text.Normalizer
import java.util.regex.Pattern

global java.util.List invalidContent

function String stripAccents(String term) {
	String str = term;
	str = Normalizer.normalize(str, Normalizer.Form.NFD);
	str = str.replaceAll("\\p{M}", "");
	return str;
}

// Drug units that require CI: abbreviations (number + optional space + unit) and full words (number + space + word).
// Add new abbreviations or full-word units here to extend the rule.
function boolean containsDrugUnit(String term) {
	if (term == null || term.isEmpty()) return false;
	String t = stripAccents(term);
	String[] abbrevs = { "mcg", "µg", "ug", "mg", "g", "kg", "ng", "pg", "mL", "ml", "L", "l", "µL", "uL", "dL", "mg/mL", "mg/L", "µg/mL", "µg/L", "ug/mL", "ug/L", "mg/kg", "µg/kg", "ug/kg", "mg/m²", "IU", "U", "IU/mL", "U/mL", "mEq", "Eq", "ppm", "%" };
	String[] fullWords = { "microgram", "milligram", "gram", "kilogram", "microgramme", "milligramme" };
	// Allowed denominators for full-word compounds (e.g. microgram/g); excludes actuation, dose, etc.
	String[] allowedDenom = { "g", "ml", "mL", "l", "L", "kg", "m2", "m²" };
	StringBuilder ab = new StringBuilder();
	for (int i = 0; i < abbrevs.length; i++) {
		if (i > 0) ab.append("|");
		ab.append(Pattern.quote(abbrevs[i]));
	}
	String abbrevPart = ab.toString();
	StringBuilder fw = new StringBuilder();
	for (int i = 0; i < fullWords.length; i++) {
		if (i > 0) fw.append("|");
		fw.append(Pattern.quote(fullWords[i]));
	}
	String fullWordPart = fw.toString();
	StringBuilder denom = new StringBuilder();
	for (int i = 0; i < allowedDenom.length; i++) {
		if (i > 0) denom.append("|");
		denom.append(Pattern.quote(allowedDenom[i]));
	}
	String denomPart = denom.toString();
	String abbrevPattern = "(?i).*\\b(?<![a-z])\\d+(?:\\.\\d+)?\\s?(" + abbrevPart + ")(?:\\/[a-z0-9]+)?\\b.*";
	// Full-word units only when compound with allowed denominator (e.g. "500 microgram/g"); exclude "microgram/actuation", "500 microgram oral capsule"
	String fullWordPattern = "(?i).*\\b(?<![a-z])\\d+(?:\\.\\d+)?\\s+(" + fullWordPart + ")\\/(" + denomPart + ")\\b.*";
	boolean abbrevMatch = t.matches(abbrevPattern);
	boolean fullWordMatch = t.matches(fullWordPattern);
	return abbrevMatch || fullWordMatch;
}

rule "An active term on an active concept with case significance 'only initial character case insensitive' should contain a capital letter after the first character."
	dialect "mvel"
	when
		c : Concept(active == true)
		d1 : Description(conceptId == c.id && active && typeId == Constants.FSN && DescriptionHelper.getTag(term) != "substance")
		d2 : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ONLY_INITIAL_CHARACTER_CASE_INSENSITIVE
						&& stripAccents(term) not matches "^..*[A-Z].*$"
						&& stripAccents(term) not matches "^..*[0-9]$"
						&& stripAccents(term) not matches "^..*[0-9][A-Za-z].*$"
						&& stripAccents(term) not matches "^[<, %, >, . , &, ^, /].*$"
						&& stripAccents(term) not matches "^..*\\p{Lu}.*$")
	then
		invalidContent.add(new InvalidContent("4ee9cfeb-3ce5-48bf-b238-de7498fde042",
				d2, "An active term on an active concept with case significance 'only initial character case insensitive' should contain " +
				"a capital letter after the first character.", Severity.WARNING));
end

rule "An active term on an active concept with case significance 'entire term case insensitive' should not contain capital letters after the first character."
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && (term matches "^..*[A-Z].*$" || stripAccents(term) matches "^..*\\p{Lu}.*$"))
	then
		invalidContent.add(new InvalidContent("900bbad3-5ebf-47ff-8206-01ff57c8f975",
				d, "An active term on an active concept with case significance 'entire term case insensitive' should not contain " +
				"capital letters after the first character.", Severity.WARNING));
end

rule "Terms containing a capital letter are expected to be marked cI or CS."
	dialect "mvel"
	when
		c : Concept(active == true)
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && (term matches "^..*[A-Z].*$" || stripAccents(term) matches "^..*\\p{Lu}.*$"))
	then
		invalidContent.add(new InvalidContent("aa0b2de4-5b51-4594-980c-3ef46b7b4ade",
				d, "Terms containing a capital letter are expected to be marked cI or CS.", Severity.WARNING));
end

rule "Terms containing drug unit abbreviations should be marked as Only initial character case insensitive."
	dialect "mvel"
	when
		c : Concept(active == true)
		fsn : Description(active, conceptId == c.id, typeId == Constants.FSN && (acceptabilityMap["900000000000509007"] == Constants.ACCEPTABILITY_PREFERRED || acceptabilityMap["900000000000508004"] == Constants.ACCEPTABILITY_PREFERRED) 
						&& DescriptionHelper.getTag(term) == "clinical drug")
		d : Description(active == true && conceptId == c.id && caseSignificanceId == Constants.ENTIRE_TERM_CASE_INSENSITIVE && containsDrugUnit(term))
	then
		invalidContent.add(new InvalidContent("3f89c2c1-3a90-4fbc-8f57-1b37fd2d0b5a",
				d, "Term using a drug unit abbreviation should be marked as Only initial character case insensitive.", Severity.WARNING));
end