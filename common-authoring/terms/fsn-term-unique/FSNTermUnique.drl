import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.service.ConceptService
import org.ihtsdo.drools.service.DescriptionService
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent
global DescriptionService descriptionService
global ConceptService conceptService

rule "Active FSNs must be unique"
		dialect "mvel"
		when
		c : Concept(active)
		d : Description(active == true && typeId == Constants.FSN && term.length > 1 && c.id == conceptId)
		d2 : Description(id != d.id && c.id != conceptId && languageCode == d.languageCode && (!d.published || (d.published && conceptService.isActive(conceptId))))
						from descriptionService.findActiveDescriptionByExactTerm(d.term)
	then
		InvalidContent invalid = new InvalidContent("d23674f3-6a36-49e3-bfa8-84375745cb33",
				d, "An FSN must be unique within all active FSNs across all concepts. This term already exists against " +
				(conceptService.isActive(d2.getConceptId()) ? "active" : "inactive") + " concept " + d2.getConceptId() + ".",
				conceptService.isActive(d2.getConceptId()) ? Severity.ERROR : Severity.WARNING);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
