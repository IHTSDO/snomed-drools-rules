import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Relationship
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.helper.RelationshipHelper
import java.util.ArrayList

global java.util.List invalidContent

rule "Active concepts must have at least one parent."
	dialect "mvel"
	when
		c : Concept(active, id != "138875005")
		activeStatedIsA: ArrayList( size == 0 )
				from collect( Relationship(active, !axiomGCI, sourceId == c.id, typeId == Constants.IS_A, characteristicTypeId == Constants.STATED_RELATIONSHIP) )
	then
		InvalidContent invalid = new InvalidContent("635de46a-3c19-4c06-8d46-3181d13d4be1",
				c, "Active concepts must have at least one IS A relationship.");
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
