import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Must have only one preferred synonym in BE_FR dialect if more than one preferred synomyms had been specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "11000172109") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0, size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["21000172104"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("4f8434d9-05ed-4e9b-b0cd-df14cb07337a",
				c, "A concept must have only one Preferred Synonym in the BE-FR dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck()
		invalidContent.add(invalid);
end

rule "Must have one preferred synonym in BE_FR dialect if there have any acceptable synonyms specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "11000172109") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["21000172104"] == Constants.ACCEPTABILITY_ACCEPTABLE)
		)
		ArrayList(size == 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["21000172104"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("0cebb78d-fd25-406b-ae84-7d2c810a9dbf",
				c, "A concept must have one Preferred Synonym in the BE-FR dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck()
		invalidContent.add(invalid);
end

rule "Preferred synonym in BE_FR dialect"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size == 0, c.moduleId == "11000172109") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "fr", acceptabilityMap["21000172104"] == Constants.ACCEPTABILITY_PREFERRED)
		)
		ArrayList(size > 0, c.moduleId == "11000172109") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "nl", (acceptabilityMap["31000172101"] == Constants.ACCEPTABILITY_PREFERRED || acceptabilityMap["31000172101"] == Constants.ACCEPTABILITY_PREFERRED))
		)
	then
		InvalidContent invalid = new InvalidContent("845d177f-93c9-49c5-9b7d-80e37b73cbd6",
				c, "A concept should have one Preferred Synonym in BE-FR dialect with the 'fr' language code.", Severity.WARNING);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
