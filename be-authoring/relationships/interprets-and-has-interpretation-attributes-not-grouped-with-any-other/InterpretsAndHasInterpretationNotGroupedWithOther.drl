import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Relationship
import org.ihtsdo.drools.response.InvalidContent

global java.util.List invalidContent

rule "Belgian Extension: Interprets and Has interpretation attributes should not be grouped with any other attributes."
	dialect "mvel"
	when
		c : Concept(active && moduleId == "11000172109")
		r1 : Relationship(active && sourceId == c.id && typeId == "363713009" /*Has interpretation (attribute)*/) 
		r2 : Relationship(active && sourceId == c.id && typeId == "363714003" /*Interprets (attribute)*/ && relationshipGroup == r1.relationshipGroup && r1.axiomId == axiomId)
		r3 : Relationship(active && sourceId == c.id && typeId != "363713009" && typeId != "363714003" && relationshipGroup == r1.relationshipGroup && r1.axiomId == axiomId)
	then
		InvalidContent invalid = new InvalidContent("9d09ab2f-14e5-4b1c-a31c-8c4fc08a2f89",
				c, "Interprets and Has interpretation attributes should not be grouped with any other attributes.");
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
