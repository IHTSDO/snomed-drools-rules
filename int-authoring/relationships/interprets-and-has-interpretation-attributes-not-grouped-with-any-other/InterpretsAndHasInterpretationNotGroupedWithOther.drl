import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Relationship
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent

global java.util.List invalidContent

rule "Interprets and Has interpretation attributes should not be grouped with any other attributes."
	dialect "mvel"
	when
		c : Concept(active)
		r1 : Relationship(active && sourceId == c.id && characteristicTypeId == Constants.STATED_RELATIONSHIP && (typeId == "363713009" || typeId == "363714003")) /*Has interpretation (attribute) || Interprets (attribute)*/
		r2 : Relationship(active && sourceId == c.id && characteristicTypeId == Constants.STATED_RELATIONSHIP && typeId != "363713009" && typeId != "363714003" && relationshipGroup == r1.relationshipGroup && r1.axiomId == axiomId)
	then
		InvalidContent invalid = new InvalidContent("ad1ccb75-7ec6-46a4-baa0-dc21bfdda257",
				c, "Interprets and Has interpretation attributes should not be grouped with any other attributes.");
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
