import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "New Austrian concepts must have one Preferred Synonym in the Austrian-German."
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "11000234105") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, moduleId == "11000234105")
		)
			
		d: ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["21000234103"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("6783b85c-e09d-4dda-b87e-a817ff5eca9b",
				c, "A concept must have only one Preferred Synonym in the DE dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
