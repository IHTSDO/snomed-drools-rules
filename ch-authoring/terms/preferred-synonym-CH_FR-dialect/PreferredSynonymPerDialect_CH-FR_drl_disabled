import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Must have one preferred synonym in CH-FR dialect if more than one preferred synomyms had been specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "2011000195101") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0, size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2021000195106"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("d4132bde-3865-489d-99d3-cafc7db2560d",
				c, "A concept must have only one Preferred Synonym in the CH-FR dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end

rule "Must have one preferred synonym in CH-FR dialect if there have any acceptable synonyms specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "2011000195101") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2021000195106"] == Constants.ACCEPTABILITY_ACCEPTABLE)
		)
		ArrayList(size == 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2021000195106"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("517d49a6-a842-47bb-a59c-0d59ac693bb9",
				c, "A concept must have one Preferred Synonym in the CH-FR dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
