import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Must have one preferred synonym in CH-DE dialect if more than one preferred synomyms had been specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "2011000195101") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0, size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2041000195100"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent(c, "A concept must have only one Preferred Synonym in the CH-DE dialect.", Severity.ERROR);
        invalid.ignorePublishedCheck();
        invalidContent.add(invalid);
end

rule "Must have one preferred synonym in CH-DE dialect if there have any acceptable synonyms specified"
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId == "2011000195101") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM)
		)
		ArrayList(size > 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2041000195100"] == Constants.ACCEPTABILITY_ACCEPTABLE)
		)
		ArrayList(size == 0) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["2041000195100"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent(c, "A concept must have one Preferred Synonym in the CH-DE dialect.", Severity.ERROR);
        invalid.ignorePublishedCheck();
        invalidContent.add(invalid);
end
