import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "All concepts must have one preferred synonym in NZ-English dialect."
	dialect "mvel"
	when
		c : Concept(active)
		d: ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, acceptabilityMap["271000210107"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("f5c7de5e-5e5b-4cf9-862c-eb8905e96199",
				c, "All concepts must have one Preferred Synonym in the NZ dialect.", Severity.ERROR);
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
