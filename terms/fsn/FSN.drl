import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import org.ihtsdo.drools.response.InvalidContent
import org.ihtsdo.drools.response.Severity

global java.util.List invalidContent

rule "Active FSNs cannot start with open parentheses"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term matches "\\(.*")
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: FSNs must not start with open parentheses."));
end

rule "FSN ends in closing parentheses"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term matches ".*[^\\)]")
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: FSNs must end in closing parentheses."));
end

rule "Active FSNs must have a space before semantic tag"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term not matches ".*[^ ] \\(.+\\).*")
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: a single space must be included before a semantic tag."));
end

rule "Active FSN must end with a semantic tag"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term not matches ".*\\(.+\\)")
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: an FSN must end with a semantic tag."));
end

rule "FSN Comma"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term matches ".*,.*")
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: FSNs must not include commas."));
end

rule "FSN space around plus character"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && (term matches ".*[^ ]\\+.*" || term matches ".*\\+[^ ].*"))
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: an active FSN containing a + symbol, must include a single space placed at either side of the symbol e.g. Ibuprofen + oxycodone (product)"));
end

rule "FSN do not contain dashes"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term matches ".*â€“.*")// En dash here
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: FSNs must not not contain dashes."));
end

rule "Active FSN starting lowercase should be case sensitive"
    dialect "mvel"
    when
        d : Description(active == true && typeId == Constants.FSN && term matches "[a-z].*" && caseSignificanceId != Constants.ENTIRE_TERM_CASE_SENSITIVE)
    then
        invalidContent.add(new InvalidContent(d, "The system has detected a contradiction of the following convention: an FSN beginning with a lowercase character must have case sensitivity of \"sensitive\".", Severity.WARNING));
end
