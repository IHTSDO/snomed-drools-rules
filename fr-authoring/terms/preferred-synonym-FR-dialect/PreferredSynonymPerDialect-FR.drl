import org.ihtsdo.drools.domain.Concept
import org.ihtsdo.drools.domain.Description
import org.ihtsdo.drools.domain.Constants
import java.util.Collection
import java.util.ArrayList
import org.ihtsdo.drools.helper.DescriptionHelper
import org.ihtsdo.drools.response.InvalidContent

global java.util.List invalidContent

rule "New French concepts must have a Preferred Synonym in French."
	dialect "mvel"
	when
		c : Concept(active)
		ArrayList(size > 0 || c.moduleId contains "11000315107") from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "fr")
		)
		ArrayList(size != 1) from collect (
			Description(active, conceptId == c.id, typeId == Constants.SYNONYM, languageCode == "fr", acceptabilityMap["10031000315102"] == Constants.ACCEPTABILITY_PREFERRED)
		)
	then
		InvalidContent invalid = new InvalidContent("37b85757-21e9-4ab2-aa47-c89b318735b6",
				c, "A concept must have one Preferred Synonym in the FR dialect.");
		invalid.ignorePublishedCheck();
		invalidContent.add(invalid);
end
